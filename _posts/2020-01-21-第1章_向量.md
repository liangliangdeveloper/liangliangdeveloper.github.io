---
layout: article
title: 第1章 向量
mathjax: true
articles:
  excerpt_type: html
tags: [代码本色, Processing]
key: c1cdnt
comment: true
---

## 1.1 向量

一个简单的弹球模拟程序：

```java
float x = 100;
float y = 100;
float xspeed = 1;
float yspeed = 3.3;

void setup(){
  size(200,200);
  smooth();
  background(255);
}

void draw(){
  background(255);
  
  x = x + xspeed;
  y = y + yspeed;
  
  if((x > width - 8)||(x < 8)){
    xspeed = xspeed * -1;
  }
  
  if((y > height - 8)||(y < 8)){
    yspeed = yspeed * -1;
  }
  
  stroke(0);
  fill(175);
  ellipse(x,y,16,16);
  
}
```

我们创建了一个到处移动的圆球。在代码中，我们用变量表示他们的属性：

> 位置：x和y
>
> 速度：xspeed和yspeed

在以后还可以加入以下变量：

> 加速度：xacceleration和yacceleration
>
> 目标位置：xtarget和ytarget
>
> 风：xwind和ywind
>
> 摩擦力：xfriction和yfriction

我们可以简化这些变量改为：

```java
Vector location;
Vector speed;
```

## 1.2 Processing变量

我们先写一个类表示向量：

```java
class PVector{
  
  float x;
  float y;
  
  PVector(float x_,float y_){
    x = x_;
    y = y_;
  }
}
```

此时位置和速度用以下方式定义：

```java
PVector location = new PVector(100,100);
PVector speed = new PVector(1,3.3);
```

## 1.3 向量的加法

```java
void add(PVector v){
    y = y + v.y;
    x = x + v.x;
    }
```

以下就是更改后的弹球程序：

```java
class PVector{
  
  float x;
  float y;
  
  PVector(float x_,float y_){
    x = x_;
    y = y_;
  }
  
  void add(PVector v){
    y = y + v.y;
    x = x + v.x;
  }
}

PVector location = new PVector(100,100);
PVector speed = new PVector(1,3.3);

void setup(){
  size(200,200);
  smooth();
  background(255);
}

void draw(){
  background(255);
  
  location.add(speed);
  
  if((location.x > width - 8)||(location.x < 8)){
    speed.x = speed.x * -1;
  }
  
  if((location.y > height - 8)||(location.y < 8)){
    speed.y = speed.y * -1;
  }
  
  stroke(0);
  fill(175);
  ellipse(location.x,location.y,16,16);
}

```

## 1.4 更多的向量运算

我们要实现以下函数：

* `add()`
* `sub()`
* `mult()`：乘上标量
* `div()`：除以标量
* `mag()`：计算向量长度

* `setMag()`
* `normalize()`
* `limit()`
* `heading2D()`

......

### 1.4.1 向量的减法

```java
void sub(PVector v){
	x = x - v.x;
	y = y - v.y;
}
```

向量代码示例代码：

```java
void setup(){
  size(200,200);
}

void draw(){
  background(255);
  PVector mouse = new PVector(mouseX,mouseY);
  PVector center = new PVector(width/2,height/2);
  
  mouse.sub(center);
  
  translate(width/2,height/2);
  
  line(0,0,mouse.x,mouse.y);
}
```

### 1.4.2 向量加减法交换律和结合律

### 1.4.3 向量的乘法

```java
void mult(float n){
	x = x * n;
	y = y * n;
}
```

### 1.4.4 向量的除法

```
void mult(float n){
	x = x / n;
	y = y / n;
}
```

## 1.5 向量的长度

```java
float mag(){
	return sqrt(x*x + y*y);
}
```

## 1.6 单位化向量

```java
void normalize(){
	float m = mag();
	if(m != 0){         //任何数不可以除以0
		div(m)
	}
}
```

## 1.7 向量的运动：速度

介绍第一个运动模拟程序（称为运动101）。

> 1. 当前位置加上新的速度得到新的位置；
>
> 2. 在新的位置上绘制对象。

我们将创建一个通用的`Mover`类，用来实现屏幕上运动的模拟：

```java
class Mover{
	PVector location;
	PVector speed;
}
```

实现的功能也很简单：

```java
void update(){
	location.add(speed);
}

void display(){
    stroke(0);
    fill(175);
    ellipse(location.x,location.y,16,16);
}
```

构造函数采用随机的方式实现：

```java
Mover(){
	location = new PVector(random(width),random(height));
	speed = new PVector(random(-2,2),random(-2,2));
}
```

最后再添加一个函数，定义对象到达屏幕边缘的行为

```java
void checkEdges(){
	if (location.x > width) {
		location.x = 0;
	} else if(location.x < 0) {
		location.x = width;
	}
	
	if (location.y > height) {
		location.y = 0;
	} else if(location.y < 0) {
		location.y = height;
	}
}
```

> **运动101**示例代码：
>
> ```java
> class PVector{
>   
>   float x;
>   float y;
>   
>   PVector(float x_,float y_){
>     x = x_;
>     y = y_;
>   }
>   
>   void add(PVector v){
>     y = y + v.y;
>     x = x + v.x;
>   }
>   
> }
> 
> class Mover{
>   PVector location;
>   PVector speed;
>   
>   Mover(){
>     location = new PVector(random(width),random(height));
>     speed = new PVector(random(-2,2),random(-2,2));
>   }
>   void update(){
>     location.add(speed);
>   }
> 
>   void display(){
>     stroke(0);
>     fill(175);
>     ellipse(location.x,location.y,16,16);
>   }
>   
>   void checkEdges(){
>     if (location.x > width) {
>       location.x = 0;
>     } else if(location.x < 0) {
>       location.x = width;
>     }
>   
>     if (location.y > height) {
>       location.y = 0;
>     } else if(location.y < 0) {
>       location.y = height;
>     }
>   }
>   
> }
> 
> Mover mover;
> 
> void setup(){
>   size(200,200);
>   smooth();
>   
>   mover = new Mover();
> }
> 
> void draw(){
>   background(255);
>   
>   mover.update();
>   mover.checkEdges();
>   mover.display();
>   
> }
> 
> ```

## 1.8 向量的运动：加速度

加速度是速度的变化率，这是一种涓滴效应。加速度影响速度，速度影响位移。代码表示如下：

```java
speed.add(acceleration);
location.add(speed);
```

我们要制定一个准则，不需要直接接触速度和位置（除了初始化）。我们设计某种加速度算法，就能完成速度和位移的计算。下面是几个获取加速度的算法：

**加速度算法**

（1）常量加速度

（2）完全随机加速度

（3）朝着鼠标方向的加速度

### 常量加速度

```java
class Mover{
	PVector location;
	PVector speed;
	
	PVector acceleration;     //加入新的加速度向量
}
```

在update中加入加速度：

```java
void update(){
	speed.add(acceleration);
	location.add(speed);
}
```

加速度应该设置初始值比较小最好，因为会有累积效应。

可以加入速度控制：

```java
speed.limit(10);
```

`limit()`函数的具体实现：

```java
void limit(float max){
	if(speed.meg() > max){
		speed.nomarlize();
		speed.mult(max);
	}
}
```

### 随机加速度

```java
PVector.random2D()           //返回一个方向任意长度为1的向量
PVector.mult(random(2))      //随机大小
```

## 1.9 静态函数和非静态函数

静态函数可以通过类名直接调用。静态的`add()`函数实现方式如下：

```java
static PVector add(PVector v1, PVector v2){
	PVector v3 = new PVector(v1.x + v2.x, v1.y + v2.y);
	return v3;
}
```

静态函数有以下特点：

* 函数被声明为*static*
* 函数返回类型不是*void*，而是一个*PVector*对象
* 函数会创建一个新的*PVector*对象（*v3*），作为两个向量相加的结果被返回

## 1.10 加速度的交互

使得物体有朝着鼠标位置的加速度：

先算出来朝向鼠标方向的向量：
$$
dx=mouseX-x
$$

$$
dy=mouseY-y
$$

```java
PVector dir = PVector.sub(mouse,location);
```

为了设置加速度向量的大小，我们必须先单位化向量，然后就可以将大小设为任意值。

```java
float anything = ???;
dir.normalize();
dir.mult(anything);
```

物体到达目标位置后不会停下来，只会超过目标位置只会再往复运动。之后需要靠减速来实现到位置之后能够停止。