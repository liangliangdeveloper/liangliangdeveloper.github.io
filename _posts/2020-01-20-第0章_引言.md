---
layout: article
title: 第0章 引言
mathjax: true
articles:
  excerpt_type: html
tags: [代码本色, Processing]
key: c0cdnt
comment: true
---



## 0.1 随机游走

| 第一次抛掷 | 第二次抛掷 |    结果    |
| :--------: | :--------: | :--------: |
|    正面    |    正面    | 向前走一步 |
|    正面    |    反面    | 向右走一步 |
|    反面    |    正面    | 向左走一步 |
|    反面    |    反面    | 向后走一步 |

以随机游走作为本书开头的3个目的：

1. 回顾面向对象编程的中心思想
2. 引入两个关键问题：如何定义对象的行为规则，如何用Processing模拟这些行为
3. 需要对随机性，概率和Perlin噪声有一些基本了解

## 0.2 随机游走类

首先我们需要构建一个Walker类：

```java
class Walker{
  int x;
  int y;
```

Walker只需要两部分数据：*x*坐标和*y*坐标.

每个类都必须包含一个构造函数，我们将Walker对象的初始位置设定在屏幕最中间：

```java
  Walker(){
    x = width/2;
    y = height/2;
  }
```

之后在类中定义一个显示自身的函数：

```java
  void display(){
    stroke(0);
    point(x, y);
  }
```

下一个函数用来控制对象的下一步移动：

```java
void step(){
    int choice = int(random(4));
    if (choice == 0) {
      x++;
    } else if (choice == 1) {
      x--;
    } else if (choice == 2) {
      y++; 
    } else {
      y--; 
    }
  }
}
```

之后就可以声明一个Walker对象：

```java
Walker w;
```

接下来就可以进行`setup()`函数和`draw()`函数的书写：

```
void setup(){
  size(640,360);
  w = new Walker();
  background(255);
}

void draw(){
  w.step();
  w.display();
}
```

最终效果如下：

<img src="\src\images\pic01.png" alt="pic1" style="zoom:75%;" />

当然也可以做一些优化，从4个方向改为9个方向。

```java
void step(){
    int stepx = int(random(3))-1;
    int stepy = int(random(3))-1;
    
    x += stepx;
    y += stepy;
  }
```

这样random可以在x轴和y轴生成三个移动方向：-1,0,1.

甚至可以生成-1到1的任意浮点数。`random()`函数的工作方式，生成的随机数是均匀分布的，可以用Sketch来测试均匀分布：

```java
float[] randomCounts;

void setup() {
  size(640,360);
  randomCounts = new float[20];
}

void draw() {
  background(255);
  
  // Pick a random number and increase the count
  int index = int(random(randomCounts.length));
  randomCounts[index]++;
  
  // Draw a rectangle to graph results
  stroke(0);
  strokeWeight(2);
  fill(127);
  
  int w = width/randomCounts.length;
  
  for (int x = 0; x < randomCounts.length; x++) {
    rect(x*w,height-randomCounts[x],w-1,randomCounts[x]);
  } 
}
```

<img src="\src\images\pic02.png" alt="pic02" style="zoom:75%;" />

> **伪随机数**
>
> `random()`的随机数并不是真正随机的，被称为“伪随机数”。这是由随机的数学函数生成的。随着时间的推移会出现固定的模式，但是那段时间很长。

## 0.3 概率和非均匀分布

如何产生一个有向右趋势的Walker，那么我们可以按照以下方式生成随机数：

```java
float prob = 0.10;

float r = random(1);
if(r < prob) {
	// try again
}
```

接下来我们创建有右移趋势的Walker：

```java
void step(){
    float r = random(1);
    if (r < 0.4) {
      x++;
    } else if (r < 0.6) {
      x--;
    } else if (r < 0.8) {
      y++; 
    } else {
      y--; 
    }
  }
```

结果如下：

<img src="\src\images\pic03.png" alt="pic03" style="zoom:75%;" />

## 0.4 随机数的正态分布

> **正态分布**：所有的观测值都聚集在平均值附近

一般用平均值μ和标准差σ来表示概率分布情况。

68%的个体分布在平均值1个标准差范围内，98%的个体分布在平均值2个标准差范围内,99.7%的个体分布在平均值3个标准差范围内.

Java中有Random类可以实现正态分布，使用Random类就要声明Random变量：

```java
void setup(){
  size(640,360);
}

void draw(){
  float num = (float) randomGaussian();
  
  float sd = 60;
  float mean = 320;
  
  float x = sd * num + mean;
  
  noStroke();
  fill(255,10);
  ellipse(x,180,16,16);
}
```

> Processing3中删除了Random类，`nextGuassian()`改名为`randomGuassian()`。

<img src="\src\images\pic04.png" alt="pic04" style="zoom:75%;" />

颜色最深的点在中间，偶尔也有一些圆在两边。

## 0.5 自定义分布的随机数

我们希望有这样一个自定义随机数：**数字越大，被选到的概率越大**

随机数为x轴，被选中的概率为y轴，建立映射：$y=x$。

如果能得到这类自定义分布的随机数生成算法，我们就可以用同样的方法计算各种公式对应的分布曲线。

常见的解决方案：生成两个随机数，第一个随机数是普通随机数，第二个随机数是“资格随机数”，用来决定第一个随机数的取舍，资格高的随机数选择概率更大，资格低的随机数选中概率更小。

> 1. 选择一个随机数R1
> 2. 计算R1选中的概率，这里P=R1
> 3. 选择另一个随机数R2
> 4. 如果R2小于P，那么R1就是我们要的随机数
> 5. 反之回到第1步开始

这称为**蒙特卡洛**算法。

## 0.6 Perlin噪声（一种更平滑的算法）

Perlin噪声算法表现出一定的自然性，能生成符合自然排序的伪随机数。Perlin噪声算法实现为`noise()`，可以有1-3个参数，分别代表一维，二维和三维。

> Processing的`noise()`函数是通过几个“八度”计算出来的，调用`noiseDetail()`改变“八度”的重要性，这会影响`noise()`函数的行为。

`noise()`函数的结果范围是固定的，会返回0~1之间的结果。

一维Perlin噪声是随着时间推移变化的线性序列，因此我们必须传入“指定的时间点”。

```java
float n = noise(t)
```

### 0.6.1 映射噪声

`map()`函数有5个参数，一个是我们想要映射的值n，剩下前两个值是本来的范围，后面两个是参数的目标范围。用到Perlin噪声的随机游走模型：

```java
class Walker{
  float x,y;
  float tx,ty;
  
  Walker(){
    tx = 0;
    ty = 10000;
  }
  
  void step(){
    x = map(noise(tx),0,1,0,width);
    y = map(noise(ty),0,1,0,height);
    
    tx += 0.01;
    ty += 0.01;
  }
}

Walker w;

void setup(){
  w = new Walker();
  size(640,360);
}

void draw(){
  noStroke();
  w.step();
  ellipse(w.x,w.y,16,16); 
}
```

之所以tx和ty从不同的地方开始，是为了得到不同的噪声，否则就会沿着对角线移动。

### 0.6.2 二维噪声

按照表格的感觉就是某一个格子和四周临近单元格数字很接近。我们可以把表格的数据可视化，把单元格上面的数字映射为色彩亮度，看着很像云状图形：

```java
float increment = 0.02;

void setup() {
  size(640,360);
  noLoop();
}

void draw() {
  background(0);
  
  // Optional: adjust noise detail here
  // noiseDetail(8,0.65f);
  
  loadPixels();

  float xoff = 0.0; // Start xoff at 0
  
  // For every x,y coordinate in a 2D space, calculate a noise value and produce a brightness value
  for (int x = 0; x < width; x++) {
    xoff += increment;   // Increment xoff 
    float yoff = 0.0;   // For every xoff, start yoff at 0
    for (int y = 0; y < height; y++) {
      yoff += increment; // Increment yoff
      
      // Calculate noise and scale by 255
      float bright = noise(xoff,yoff)*255;

      // Try using this line instead
      //float bright = random(0,255);
      
      // Set each pixel onscreen to a grayscale value
      pixels[x+y*width] = color(bright);
    }
  }
  
  updatePixels();
}
```

<img src="\src\images\pic05.png" alt="pic05" style="zoom:75%;" />

## 0.7 前进

本章讨论了如何用随机数在模拟过程中扮演万能角色。但是Perlin噪声不是万能的。本书的目的是扩充你的工具箱。